
#if __windows__
    foreign GetModuleFileNameA(ptr, *ubyte, uint) uint
    func getExecutableLocation String {
        // Returns location of executable on windows
        // NOTE: Returns empty string on failure
        result POD String = undef
        result.array = new ubyte * 1024
        result.capacity = 1024
        result.ownership = StringOwnership::GIVEN
        result.length = GetModuleFileNameA(null, result.array, 1024) as usize
        
        if result.length == 0 {
            delete result.array
            return ""
        }
        
        return result.sub(0, result.last('\\'ub) + 1)
    }
#elif __macos__
    foreign _NSGetExecutablePath(*ubyte, *uint) int
    foreign realpath(*ubyte, *ubyte) *ubyte
    
    func getExecutableLocation String {
        // Returns location of executable on mac
        // NOTE: Returns empty string on failure
        
        storage 1024 ubyte = undef
        size uint = 1024
        
        if _NSGetExecutablePath(&storage as *ubyte, &size) != 0,
            return ""
        
        resolved *ubyte = new undef ubyte * 4096
        realpath(&storage as *ubyte, resolved)
        return stringTake(resolved, 4096)
    }
#else
    #print_error "'2.4/where.adept': Only supported on Windows and MacOS"
    #halt
#end

func where String {
    // Returns path that executable resides in
    // NOTE: Returns empty string on failure
    
    executable String = getExecutableLocation()
    last_slash long = max(executable.last('/'ub), executable.last('\\'ub))
    if last_slash == -1, return ""
    
    return executable.sub(0, (last_slash + 1) as usize)
}
