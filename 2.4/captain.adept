
pragma compiler_supports '2.4'

import math
import random
import String
import Matrix4f

import 'glfw/glfw.adept'
import 'opengl/opengl.adept'
import 'stb/image.adept'
import 'sys/ctime.adept'
import 'sys/cstdio.adept'
import 'sys/cstring.adept'

_captain_prepared bool
_captain_window *GLFWwindow
_captain_setup func() void
_captain_exit func() void
_captain_draw func() void
_captain_step func() void
_captain_step_fps uint
_captain_click func(float, float, int) void
_captain_click_use_view_mouse bool
_captain_release func(float, float, int) void
_captain_release_use_view_mouse bool
_captain_key func(int, int, int, int) void
_captain_char func(uint) void
_captain_scroll func(float, float) void
_captain_shader CaptShader
_captain_display_ratio float
_captain_view_width float
_captain_view_height float
_captain_custom_view bool
_captain_custom_view_ratio float
_captain_view_preserve_ratio bool
_captain_resizable bool
_captain_always_on_top bool
_captain_model CaptModel
_captain_shader_transformation_location GLint
_captain_multisample int

func captPrepare successful {
    _captain_window = null
    _captain_setup = null
    _captain_exit = null
    _captain_draw = null
    _captain_step = null
    _captain_step_fps = 60
    _captain_click = null
    _captain_release = null
    _captain_key = null
    _captain_char = null
    _captain_scroll = null
    _captain_display_ratio = 0.0f
    _captain_view_width = 0.0f
    _captain_view_height = 0.0f
    _captain_custom_view = false
    _captain_custom_view_ratio = 1.0f
    _captain_view_preserve_ratio = false
    _captain_resizable = true
    _captain_shader_transformation_location = 0
    _captain_multisample = 0
    _captain_prepared = true
    return glfwInit() != 0
}

func captStart(in fullscreen bool) {
    if fullscreen, captStart('', 0, 0, true)
    else           captStart('', 640, 480, false)
}

func captStart(in title *ubyte = '', in fullscreen bool = false) {
    if fullscreen, captStart(title, 0, 0, true)
    else           captStart(title, 640, 480, false)
}

func captEnsurePrepared {
    if _captain_prepared || captPrepare(), return
    printf('RUNTIME ERROR: captPrepare() failed\n')
    return
}

func captStart(in title *ubyte, in width, height int, in fullscreen bool) {
    captEnsurePrepared()
    
    #if __macos__
    glfwWindowHint(GLFW_CONTEXT_VERSION_MAJOR, 3);
    glfwWindowHint(GLFW_CONTEXT_VERSION_MINOR, 2);
    glfwWindowHint(GLFW_OPENGL_FORWARD_COMPAT, GL_TRUE);
    glfwWindowHint(GLFW_OPENGL_PROFILE, GLFW_OPENGL_CORE_PROFILE);
    #end
    
    unless _captain_resizable {
        glfwWindowHint(GLFW_RESIZABLE, GL_FALSE)
    }
    
    if _captain_always_on_top {
        glfwWindowHint(GLFW_FLOATING, GL_TRUE)
    }

    if _captain_multisample != 0 {
        glfwWindowHint(GLFW_SAMPLES, _captain_multisample)
    }
    
    monitor *GLFWmonitor = null
    
    if fullscreen {
        monitor = glfwGetPrimaryMonitor()
    }
    
    if width == 0 && height == 0 {
        video_mode *GLFWvidmode = glfwGetVideoMode(monitor)
        width = video_mode.width
        height = video_mode.height
    }
    
    _captain_window = glfwCreateWindow(width, height, title, monitor, null)
    
    if _captain_window == null {
        glfwTerminate()
        return
    }
    
    glfwMakeContextCurrent(_captain_window)
    glfwSwapInterval(1)
    openglInit()
    
    glEnable(GL_BLEND)
    glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA)
    if _captain_multisample != 0, glEnable(GL_MULTISAMPLE)
    
    _captain_shader.create("#version 400 core\nlayout(location = 0) in vec3 vp;\nlayout(location = 1) in vec2 texture_coords;\nuniform mat4 projection;\nuniform mat4 transformation;\nout vec2 uvs;\nvoid main() {\nuvs = texture_coords;\ngl_Position = projection * transformation * vec4(vp, 1.0);\n}",
            "#version 400 core\nin vec2 uvs;\nout vec4 color;\nuniform sampler2D texture_sampler;\nuniform float opacity;\nvoid main(void) {\ncolor = texture(texture_sampler, uvs);\ncolor.a *= opacity;\n}")
    _captain_shader_transformation_location = _captain_shader.getUniformLocation('transformation')
    _captain_model = captModel(1.0f, 1.0f)
    
    glfwGetFramebufferSize(_captain_window, undef win_width int, undef win_height int)
    _captain_display_ratio = cast float win_width / cast float win_height
        
    glfwSetMouseButtonCallback(_captain_window, func &_captMouseCallback)
    glfwSetKeyCallback(_captain_window, func &_captKeyCallback)
    glfwSetCharCallback(_captain_window, func &_captCharCallback)
    glfwSetScrollCallback(_captain_window, func &_captScrollCallback)
    
    unless _captain_custom_view, captDefaultView()    
    if _captain_setup as ptr, _captain_setup()
    
    previous_time double = glfwGetTime()
    projection Matrix4f
    
    until glfwWindowShouldClose(_captain_window) {
        if glfwGetTime() - previous_time > 1.0 / cast float _captain_step_fps {
            if _captain_step as ptr, _captain_step()
            previous_time += 1.0 / cast float _captain_step_fps
        }
        
        glClearColor(0.0f, 0.0f, 0.0f, 1.0f)
        glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)
    
        glfwGetFramebufferSize(_captain_window, &win_width, &win_height)
        glViewport(0, 0, win_width, win_height)
        _captain_display_ratio = cast float win_width / cast float win_height
        
        unless _captain_custom_view, captDefaultView()
        if _captain_view_preserve_ratio, _captain_view_width = _captain_view_height * _captain_display_ratio
        
        projection.ortho(0.0f, _captain_view_width, _captain_view_height, 0.0f, 1.0f, -1.0f)
        
        _captain_shader.use()
        _captain_shader.uploadMatrix4f(_captain_shader.getUniformLocation('projection'), projection)
        _captain_shader.uploadFloat(_captain_shader.getUniformLocation('opacity'), 1.0f)
        
        if _captain_draw as ptr, _captain_draw()
        
        glfwSwapBuffers(_captain_window)
        glfwPollEvents()
    }
    
    if _captain_exit as ptr, _captain_exit()
    
    _captain_model.destroy()
    _captain_shader.destroy()
    
    glfwDestroyWindow(_captain_window)
    glfwTerminate()
}

func captDefaultView {
    captCustomViewBasedOnHeight(480.0f)
}

func _captMouseCallback(_window *GLFWwindow, button int, action int, _mods int) {
    if action == GLFW_PRESS && _captain_click as ptr {
        mouse_x, mouse_y float = undef
        if _captain_click_use_view_mouse, captMouseViewPosition(&mouse_x, &mouse_y)
        else                              captMouseFramePosition(&mouse_x, &mouse_y)
        
        _captain_click(mouse_x, mouse_y, button + 1)
    } else if action == GLFW_RELEASE && _captain_release as ptr {
        mouse_x, mouse_y float = undef
        if _captain_release_use_view_mouse, captMouseViewPosition(&mouse_x, &mouse_y)
        else                                captMouseFramePosition(&mouse_x, &mouse_y)
        
        _captain_release(mouse_x, mouse_y, button + 1)
    }
}

func _captKeyCallback(_window *GLFWwindow, k, s, a, m int) {
    if _captain_key as ptr, _captain_key(k, s, a, m)
}

func _captCharCallback(_window *GLFWwindow, codepoint uint) {
    if _captain_char as ptr, _captain_char(codepoint)
}

func _captScrollCallback(_window *GLFWwindow, x_offset double, y_offset double) {
    if _captain_scroll as ptr, _captain_scroll(x_offset as float, y_offset as float)
}

func captViewWidth float {
    return _captain_view_width
}

func captViewHeight float {
    return _captain_view_height
}

func captFrameWidth int {
    w, h int = undef
    glfwGetFramebufferSize(_captain_window, &w, &h)
    return w
}

func captFrameHeight int {
    w, h int = undef
    glfwGetFramebufferSize(_captain_window, &w, &h)
    return h
}

func captCustomView(width, height float) {
    captEnsurePrepared()
    _captain_view_width = width
    _captain_view_height = height
    _captain_custom_view = true
    _captain_view_preserve_ratio = false
}

func captCustomViewBasedOnHeight(height float) {
    captEnsurePrepared()
    _captain_view_width = height * _captain_display_ratio
    _captain_view_height = height
    _captain_custom_view = true
    _captain_view_preserve_ratio = true
}

func captMouseFramePosition(out x, y *float) {
    glfwGetCursorPos(_captain_window, undef x_d double, undef y_d double)
    *x = x_d as float
    *y = y_d as float
}

func captMouseFramePosition(out x, y *double) {
    glfwGetCursorPos(_captain_window, x, y)
}

func captMouseViewPosition(out x, y *float) {
    glfwGetCursorPos(_captain_window, undef x_d double, undef y_d double)
    *x = x_d as float * captViewWidth() / captFrameWidth() as float
    *y = y_d as float * captViewHeight() / captFrameHeight() as float
}

func captResizable(resizable bool) {
    captEnsurePrepared()
    _captain_resizable = resizable
}

func captAlwaysOnTop(always_on_top bool) {
    captEnsurePrepared()
    _captain_always_on_top = always_on_top
}

func captMultisample(multisample_rate int) {
    captEnsurePrepared()
    _captain_multisample = multisample_rate
}

func captHideCursor(hide bool) {
    captEnsurePrepared()
    glfwSetInputMode(_captain_window, GLFW_CURSOR, hide ? GLFW_CURSOR_HIDDEN : GLFW_CURSOR_NORMAL)
}

func captOnSetup(in on_setup_func func() void) {
    captEnsurePrepared()
    _captain_setup = on_setup_func
}

func captOnExit(in on_exit_func func() void) {
    captEnsurePrepared()
    _captain_exit = on_exit_func
}

func captOnDraw(in on_draw_func func() void) {
    captEnsurePrepared()
    _captain_draw = on_draw_func
}

func captOnStep(in on_step_func func() void) {
    captEnsurePrepared()
    _captain_step = on_step_func
    _captain_step_fps = 60
}

func captOnStep(in on_step_func func() void, in step_fps uint) {
    captEnsurePrepared()
    _captain_step = on_step_func
    _captain_step_fps = step_fps == 0 ? 1000000 : step_fps
}

func captOnClick(in on_click_func func(float, float, int) void, use_mouse_view_coords bool) {
    captEnsurePrepared()
    _captain_click = on_click_func
    _captain_click_use_view_mouse = use_mouse_view_coords
}

func captOnRelease(in on_release_func func(float, float, int) void, use_mouse_view_coords bool) {
    captEnsurePrepared()
    _captain_release = on_release_func
    _captain_release_use_view_mouse = use_mouse_view_coords
}

func captOnKey(in on_key_func func(int, int, int, int) void) {
    captEnsurePrepared()
    _captain_key = on_key_func
}

func captOnChar(in on_char_func func(uint) void) {
    captEnsurePrepared()
    _captain_char = on_char_func
}

func captOnScroll(in on_scroll_func func(float, float) void) {
    captEnsurePrepared()
    _captain_scroll = on_scroll_func
}

struct CaptColor (r, g, b, a float)

func captColor(r, g, b float, a float = 1.0f) CaptColor {
    color CaptColor = undef
    color.r = r
    color.g = g
    color.b = b
    color.a = a
    return color
}

func captColor(r, g, b int, a int = 255) CaptColor {
    color CaptColor = undef
    color.r = cast float r / 255.0f
    color.g = cast float g / 255.0f
    color.b = cast float b / 255.0f
    color.a = cast float a / 255.0f
    return color
}

func captClearColor(in color CaptColor) {
    glClearColor(color.r, color.g, color.b, color.a)
    glClear(GL_COLOR_BUFFER_BIT)
}

struct CaptTexture (id uint) {
    func isValid bool = this.id != 0
    
    func invalidate {
        this.id = 0
    }
    
    func destroy {
        glDeleteTextures(1, &this.id)
    }
    
    func load(filename *ubyte, approximate bool = true) {
        *this = captTexture(filename, approximate)
    }
    
    func load(filename String, approximate bool = true) {
        cstr *ubyte = filename.cstr()
        defer delete cstr
        *this = captTexture(cstr, approximate)
    }
}

func captTexture(filename String, approximate bool = true) CaptTexture {
    cstr *ubyte = filename.cstr()
    defer delete cstr
    return captTexture(cstr, approximate)
}

func captTexture(filename *ubyte, approximate bool = true) CaptTexture {
    captEnsurePrepared()
    texture POD CaptTexture = undef
    
    if _captain_window == null {
        printf('RUNTIME ERROR: Cannot create CaptTexture before captStart() is called\n')
        printf('NOTE: Use captOnSetup() to load assets after captStart()\n')
        texture.id = 0
        return texture
    }
    
    x, y, n int = undef
    image *ubyte = stbi_load(filename, &x, &y, &n, 4)

    if image == null {
        texture.id = 0
        return texture
    }
    
    blur_mode GLenum = approximate ? GL_LINEAR : GL_NEAREST
    glGenTextures(1, &texture.id)
    glBindTexture(GL_TEXTURE_2D, texture.id)
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, blur_mode)
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, blur_mode)
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE)
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE)
    glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA, x, y, 0, GL_RGBA, GL_UNSIGNED_BYTE, image)
    glBindTexture(GL_TEXTURE_2D, 0ui)
    stbi_image_free(image)
    return texture
}

struct CaptModel (vao, vertices_vbo, uvs_vbo, texture_id GLuint, triangle_count int) {
    func create(points *float, points_length usize, uvs *float, uvs_length usize) {
        this.triangle_count = cast int(points_length / 3ui)
        
        this.vertices_vbo = 0ui
        glGenBuffers(1, &this.vertices_vbo)
        glBindBuffer(GL_ARRAY_BUFFER, this.vertices_vbo)
        glBufferData(GL_ARRAY_BUFFER, cast int(sizeof float * points_length), points, GL_STATIC_DRAW)
        
        this.uvs_vbo = 0ui
        glGenBuffers(1, &this.uvs_vbo)
        glBindBuffer(GL_ARRAY_BUFFER, this.uvs_vbo)
        glBufferData(GL_ARRAY_BUFFER, cast int(sizeof float * uvs_length), uvs, GL_STATIC_DRAW)
        
        glGenVertexArrays(1, &this.vao)
        glBindVertexArray(this.vao)
        
        glEnableVertexAttribArray(0ui)
        glBindBuffer(GL_ARRAY_BUFFER, this.vertices_vbo)
        glVertexAttribPointer(0ui, 3, GL_FLOAT, GL_FALSE, 0, null)
        
        glEnableVertexAttribArray(1ui)
        glBindBuffer(GL_ARRAY_BUFFER, this.uvs_vbo)
        glVertexAttribPointer(1ui, 2, GL_FLOAT, GL_FALSE, 0, null)
        
        glBindBuffer(GL_ARRAY_BUFFER, 0ui)
        glBindVertexArray(0ui)
    }
    
    func draw {
        glBindVertexArray(this.vao)
        glDrawArrays(GL_TRIANGLES, 0, this.triangle_count)
    }
    
    func destroy {
        glDeleteBuffers(1, &this.vertices_vbo)
        glDeleteBuffers(1, &this.uvs_vbo)
        glDeleteVertexArrays(1, &this.vao)
    }
}

func captModel(points <float> List, uvs <float> List) CaptModel {
    model POD CaptModel
    model.create(points.__array__(), points.__length__(), uvs.__array__(), uvs.__length__())
    return model
}

func captBindTexture(texture POD CaptTexture) void {
    glActiveTexture(GL_TEXTURE0)
    glBindTexture(GL_TEXTURE_2D, texture.id)
}

struct CaptShader (program, vertex, fragment GLuint) {
    func create(vertex_code, fragment_code String) {
        log_length int
        log_message *ubyte
        status GLint
        code_length GLint = undef
    
        this.vertex = glCreateShader(GL_VERTEX_SHADER)
        code_length = vertex_code.length
        glShaderSource(this.vertex, 1, &vertex_code.array, &code_length)
        glCompileShader(this.vertex)
        glGetShaderiv(this.vertex, GL_COMPILE_STATUS, &status)
    
        if status == 0 {
            puts('Failed to compile vertex shader')
            log_message = malloc(1024)
            glGetShaderInfoLog(this.vertex, 1024, &log_length, log_message)
            if log_length != 0, puts(log_message)
            free(log_message)
        }
    
        this.fragment = glCreateShader(GL_FRAGMENT_SHADER)
        code_length = fragment_code.length
        glShaderSource(this.fragment, 1, &fragment_code.array, &code_length)
        glCompileShader(this.fragment)
        glGetShaderiv(this.fragment, GL_COMPILE_STATUS, &status)
        
        if status == 0 {
            puts('Failed to compile fragment shader')
            log_message = malloc(1024)
            glGetShaderInfoLog(this.fragment, 1024, &log_length, log_message)
            if log_length != 0, puts(log_message)
            free(log_message)
        }
    
        this.program = glCreateProgram()
        glAttachShader(this.program, this.vertex)
        glAttachShader(this.program, this.fragment)
        glLinkProgram(this.program)
        glValidateProgram(this.program)
    }
    
    func use {
        glUseProgram(this.program)
    }
    
    func destroy {
        glDetachShader(this.program, this.vertex)
        glDetachShader(this.program, this.fragment)
        glDeleteShader(this.vertex)
        glDeleteShader(this.fragment)
        glDeleteProgram(this.program)
    }
    
    func getUniformLocation(name *ubyte) GLint {
        return glGetUniformLocation(this.program, name)
    }

    func uploadFloat(location GLint, value float) {
        glUniform1f(location, value)
    }
    
    func uploadMatrix4f(location GLint, matrix *Matrix4f) {
        if matrix == null, return
        glUniformMatrix4fv(location, 1, false, &matrix.array as *float)
    }
    
    func uploadMatrix4f(location GLint, matrix Matrix4f) {
        glUniformMatrix4fv(location, 1, false, &matrix.array as *float)
    }
}

func captShader(vertex_shader_code, fragment_shader_code String) CaptShader {
    shader POD CaptShader
    shader.create(vertex_shader_code, fragment_shader_code)
    return shader
}

func captDrawTexture(texture POD CaptTexture, in x, y, w, h float) {
    transformation Matrix4f = undef
    transformation.translateFromIdentity(x, y, 0.0f)
    transformation.scale(w, h, 1.0f)
    _captain_shader.uploadMatrix4f(_captain_shader_transformation_location, transformation)
    captBindTexture(texture)
    _captain_model.draw()
}

func captDrawTextureUsingModel(model POD CaptModel, texture POD CaptTexture, in x, y float) {
    transformation Matrix4f = undef
    transformation.translateFromIdentity(x, y, 0.0f)
    _captain_shader.uploadMatrix4f(_captain_shader_transformation_location, transformation)
    captBindTexture(texture)
    model.draw()
}

func captDrawTextureUsingModelAndTransformation(model POD CaptModel, texture POD CaptTexture, transformation Matrix4f) {
    _captain_shader.uploadMatrix4f(_captain_shader_transformation_location, transformation)
    captBindTexture(texture)
    model.draw()
}

func captDrawOpacity(in value float) {
    _captain_shader.uploadFloat(_captain_shader.getUniformLocation('opacity'), value)
}

func captModel(in w, h float, in x_flip, y_flip bool = false) CaptModel {
    points_storage 18 float = undef
    uv_data_storage 12 float = undef

    points *float = &points_storage as *float
    points_length usize = 18
    uv_data *float = &uv_data_storage as *float
    uv_data_length usize = 12
    
    points[0]  = 0.0f;   points[1]  = h;      points[2]  = 0.0f
    points[3]  = 0.0f;   points[4]  = 0.0f;   points[5]  = 0.0f
    points[6]  = w;      points[7]  = 0.0f;   points[8]  = 0.0f
    points[9]  = 0.0f;   points[10] = h;      points[11] = 0.0f
    points[12] = w;      points[13] = 0.0f;   points[14] = 0.0f
    points[15] = w;      points[16] = h;      points[17] = 0.0f

    // 2 3
    // 1 6
    
    // 1, 2, 3, 1, 3, 4
    uv_src 8 float = undef
    uv_src[0]  = 0.0f; uv_src[1]  = 1.0f
    uv_src[2]  = 0.0f; uv_src[3]  = 0.0f
    uv_src[4]  = 1.0f; uv_src[5]  = 0.0f
    uv_src[6]  = 1.0f;  uv_src[7] = 1.0f
    
    if x_flip {
        // Swap 2 with 3 and 1 with 6
        tmp 2 float = undef
        tmp[0] = uv_src[2]
        tmp[1] = uv_src[3]
        uv_src[2] = uv_src[4]
        uv_src[3] = uv_src[5]
        uv_src[4] = tmp[0]
        uv_src[5] = tmp[1]
        tmp[0] = uv_src[0]
        tmp[1] = uv_src[1]
        uv_src[0] = uv_src[6]
        uv_src[1] = uv_src[7]
        uv_src[6] = tmp[0]
        uv_src[7] = tmp[1]
    }
    
    if y_flip {
        // Swap 2 with 1 and 3 with 6
        tmp 2 float = undef
        
        tmp[0] = uv_src[2]
        tmp[1] = uv_src[3]
        uv_src[2] = uv_src[0]
        uv_src[3] = uv_src[1]
        uv_src[0] = tmp[0]
        uv_src[1] = tmp[1]
        
        tmp[0] = uv_src[4]
        tmp[1] = uv_src[5]
        uv_src[4] = uv_src[6]
        uv_src[5] = uv_src[7]
        uv_src[6] = tmp[0]
        uv_src[7] = tmp[1]
    }
    
    uv_data[0]  = uv_src[0]; uv_data[1]  = uv_src[1]
    uv_data[2]  = uv_src[2]; uv_data[3]  = uv_src[3]
    uv_data[4]  = uv_src[4]; uv_data[5]  = uv_src[5]
    uv_data[6]  = uv_src[0]; uv_data[7]  = uv_src[1]
    uv_data[8]  = uv_src[4]; uv_data[9]  = uv_src[5]
    uv_data[10] = uv_src[6]; uv_data[11] = uv_src[7]

    model CaptModel
    model.create(points, points_length, uv_data, uv_data_length)
    return model
}

func alias captRandomize() => randomize
func alias captRandom() => normalizedRandom

func captKeyHeld(key int) bool {
    return glfwGetKey(_captain_window, key) == GLFW_PRESS
}
