
#default Array_bounds_checks true

import InitializerList

record <$T> Array (items *$T, length usize) {
    func __array__ *$T               = this.items
    func __length__ usize            = this.length
    func __access__(index usize) *$T {
        #if Array_bounds_checks
            if index < 0 || index >= this.length {
                printf('<%s> Array Error: [] index out of range\n', typenameof $T)
                return this.items
            }
        #end
        
        return this.items at index
    }
}

func get(this *<$T> Array, index usize) $T {
    #if Array_bounds_checks
        if index < 0 || index >= this.length {
            printf('<%s> Array Error: get() index out of bounds\n', typenameof $T)
            return *this.items
        }
    #end
    
    return this.items[index]
}

func getPointer(this *<$T> Array, index usize) *$T {
    #if Array_bounds_checks
        if index < 0 || index >= this.length {
            printf('<%s> Array Error: getPointer() index out of bounds\n', typenameof $T)
            return this.items
        }
    #end

    return this.items at index
}

func contains(this *<$T> Array, value $~T) bool {
    each $T in static *this, if it == value, return true
    return false
}

func applyDefer(this *<$T> Array) {
    each $T in static *this, it.__defer__()
}

implicit func __as__(initializer <$T> InitializerList) <$T> Array {
    return array(initializer.array, initializer.length)
}

// Allow <long> InitializerList for <int> List
implicit func __as__(initializer <long> InitializerList) <int> Array     = initializer as <int> InitializerList as <int> Array

// Allow <double> InitializerList for <float> List
implicit func __as__(initializer <double> InitializerList) <float> Array = initializer as <float> InitializerList as <float> Array
